SELECT ?dataset ?title ?landingPage
((group_concat(distinct ?themeLabel ; separator = ","))  as ?themes)
((group_concat(distinct ?kw ; separator = ",")) as ?keywords)
((group_concat(distinct ?content ; separator = ",")) as ?natureOfContent)
?publisher ?language ?licenseLabel
?dateIssued ?dateModified
?spatial ?tStartDate
?tEndDate ?datasetDescription
?publisherName ?publisherURL
?accessMethod ?license
WHERE {
	?dataset a <http://www.w3.org/ns/dcat#Dataset> ;
	<http://www.w3.org/ns/dcat#distribution>  [
		a		<http://www.w3.org/ns/dcat#Distribution> ;
		<http://www.w3.org/ns/dcat#mediaType> ?accessMethod ;
	] ;
	<http://purl.org/dc/terms/title> ?title ;
	<http://www.w3.org/ns/dcat#landingPage> ?landingPage ;
	<http://www.w3.org/ns/dcat#theme> [
	a <http://www.w3.org/2008/05/skos#Concept> ;
	<http://www.w3.org/2008/05/skos#prefLabel> ?themeLabel ] ;
	<http://www.w3.org/ns/dcat#keyword> ?kw ;
	<http://purl.org/dc/terms/publisher> ?publisher ;
	<http://purl.org/dc/terms/language> ?language ;
	<http://purl.org/dc/terms/license> ?license ;
	<http://purl.org/dc/terms/description> ?datasetDescription .
	?publisher <http://xmlns.com/foaf/0.1/name> ?publisherName ;
		<http://xmlns.com/foaf/0.1/homepage> ?publisherURL .

	OPTIONAL{ ?dataset 	<http://example.org/dso#content> ?cnt } .
	OPTIONAL{ ?dataset 	<http://purl.org/dc/terms/issued> ?dateIssued } .
	OPTIONAL{ ?dataset 	<http://purl.org/dc/terms/modified> ?dateModified } .
	OPTIONAL{ ?dataset 	<http://purl.org/dc/terms/spatial> ?spatial } .
	OPTIONAL{ ?dataset <http://purl.org/dc/terms/temporal>  [
		a <http://purl.org/dc/terms/PeriodOfTime> ;
		<http://schema.org/startDate> ?tStartDate ;
		<http://schema.org/endDate> ?tEndDate    ] } .

	GRAPH <http://example.org/dso#> {
		?cnt a <http://www.w3.org/2008/05/skos#Concept> .
		?cnt <http://www.w3.org/2008/05/skos#prefLabel> ?content .
	}

	GRAPH <http://purl.org/NET/rdflicense/> {
		?license <http://www.w3.org/2000/01/rdf-schema#label> ?licenseLabel .
	}
}
GROUP BY
?dataset ?title ?landingPage
?publisher ?language
?licenseLabel ?dateIssued
?dateModified ?tStartDate ?tEndDate
?spatial ?datasetDescription
?publisherName ?publisherURL
?accessMethod ?license
